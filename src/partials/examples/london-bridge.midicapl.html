INSTRUMENTS
	4  LEAD_VOICE  Vocals
END

CALL vocals(Lon, don_, Bridge_, is_, fal, ling_, down)
CALL vocals(Build_, it_, up_, with_, wood_, and_, clay)
CALL vocals(Wood_, and_, clay_, will_, wash_, a, way)

FUNCTION vocals
	CALL measure_1($[0], $[1], $[2], $[3])  // London bridge is
	CALL measure_2($[4], $[5], $[6]\c\r)    // falling down,
	CALL measure_3($[4], $[5], $[6]\c_)     // falling down,
	CALL measure_2($[4], $[5], $[6].\r)     // falling down.
	CALL measure_1($[0], $[1], $[2], $[3])  // London bridge is
	CALL measure_2($[4], $[5], $[6].\r)     // falling down,
	CALL refrain()                          // My fair lady.
END
FUNCTION measure_1
	4  a  /4.  l=$[0]    // Lon-
	4  b  /8   l=$[1]    // don
	4  a  /4   l=$[2]    // bridge
	4  g  /4   l=$[3]    // is
END
FUNCTION measure_2
	4  f#  /4   l=$[0]    // fal-
	4  g   /4   l=$[1]    // ling
	4  a   /2   l=$[2]    // down
END
FUNCTION measure_3
	4  e   /4   l=$[0]    // fal-
	4  f#  /4   l=$[1]    // ling
	4  g   /2   l=$[2]    // down
END
FUNCTION refrain
	4  e   /2   l=My_
	4  a   /2   l=fair_
	4  f#  /4   l=La
	4  d   /2.  l=dy.\n
END